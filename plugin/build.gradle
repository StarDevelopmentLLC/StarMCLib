import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'com.gradleup.shadow' version '9.2.2'
    id 'xyz.kyngs.libby.plugin' version '2.0.1'
}

version = rootProject.version

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://repo.stardevllc.com/releases'}
}

dependencies {
    implementation("com.stardevllc.libby:libby-bukkit:1.0.1")
    implementation project(':common')
    compileOnly "org.spigotmc:spigot-api:${spigotVersion}"
    implementation "com.stardevllc:StarLib:${starlibVersion}"
    implementation "com.stardevllc:SMCVersion:${smcvVersion}"
    implementation "com.stardevllc:config:${configVersion}"
    libby "com.google.code.gson:gson:${gsonVersion}"
    libby "net.kyori:adventure-api:${adventureVersion}"
    libby "net.kyori:adventure-platform-bukkit:${adventureBukkitVersion}"
    libby "net.kyori:adventure-text-minimessage:${miniMessageVersion}"
    libby "org.yaml:snakeyaml:${snakeymlVersion}"
}

libby {
    excludeDependency("com.google.errorprone:.*:.*")
    excludeDependency("org.jetbrains:.*:.*")
}

processResources {
    filesMatching("*.yml") {
        expand(project.properties)
    }
}

tasks.named('shadowJar', ShadowJar) {
    archiveClassifier = 'plugin'
    archiveBaseName = 'StarMCLib'
    destinationDirectory = rootProject.layout.buildDirectory.dir("libs")
    dependencies {
        exclude(dependency("org.jetbrains:annotations:.*"))
        exclude(dependency("com.google.errorprone:error_prone_annotations:.*"))
    }
    
    relocate('net.kyori', 'com.stardevllc.kyori')
    relocate('com.google.gson', 'com.stardevllc.gson')
    relocate('org.yaml.snakeyaml', 'com.stardevllc.snakeyaml')
    relocate("net.byteflux.libby", "com.stardevllc.starmclib.libby")
    relocate('com.grack', 'com.stardevllc.starmclib.grack')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }

    withJavadocJar()
    withSourcesJar()
}

tasks.compileJava.dependsOn(clean)